config = (import | from_import | config_import)* definition*

config_import = import_str | import_starred
import_str = ('from' STRING)? 'import' paths | (PAR_OPEN paths PAR_CLOSE)
import_starred = 'from' IDENTIFIER? '.' dotted 'import' '*'
paths = (STRING ','?)+

import = 'import' dotted_import_as (',' dotted_import_as)* ','?
from_import = 'from' dotted 'import' import_as (',' import_as)* ','?

dotted = IDENTIFIER ('.' IDENTIFIER)*
import_as = IDENTIFIER (as IDENTIFIER)?
dotted_import_as = dotted (as IDENTIFIER)?

definition = IDENTIFIER '=' expression

expression = data (getAttribute | call | getItem)*
getItem = '[' expression (',' expression)* ','? ']'
getAttribute = '.' IDENTIFIER
call = PAR_OPEN params PAR_CLOSE
params = LAZY? (positional | keyword | (positional ',' keyword) ','?)?
positional = '*'? expression (',' '*'? expression)*
keyword = definition (',' definition)*

data = module | dictionary | array | IDENTIFIER | STRING | NUMBER | 'None' | 'False' | 'True'
module = IDENTIFIER ':' IDENTIFIER
dictionary = '{' (pair (',' pair)* ','?)? '}'
pair = expression ':' expression
array = '[' (expression (',' expression)* ','?)? ']'

// this is an approximate regex. the true one is simply imported from `tokenize`
STRING = r'''(\"\"\"|\'\'\'|\"|\')((?<!\\)(\\\\)*\\\1]|.)*?\1'''
NUMBER = r'-?(0|[1-9][0-9]*)(\.[0-9]+)?([eE][+-]?[0-9]+)?'

IDENTIFIER = r'[^\d\W]\w*'
PAR_OPEN = '('
PAR_CLOSE = ')'
LAZY = '#\s*lazy\s*$'
COMMENT = r'#.*$'
