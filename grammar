config = extends* definition*

extends = DIRECTIVE ('from' STRING)? 'extends' paths | (LAMBDA_OPEN paths LAMBDA_CLOSE)
paths = (STRING ','?)+

definition = IDENTIFIER '=' expression

expression = simpleBlock | (simpleBlock getAttribute tail) | (module tail)
simpleBLock = data (getAttribute | partial)*
getAttribute = '.' IDENTIFIER
partial = LAMBDA_OPEN params LAMBDA_CLOSE
tail = BLOCK_OPEN params BLOCK_CLOSE

params = DIRECTIVE 'lazy'? (definition ','?)*

data = module | IDENTIFIER | object | array | STRING | NUMBER | 'null' | 'false' | 'true'
module = IDENTIFIER ':' IDENTIFIER partial?
object = '{' (STRING ':' expression ','?)* '}'
array = '[' (expression ','?)* ']'

STRING = r'"(\\([\"\\\/bfnrt]|u[a-fA-F0-9]{4})|[^\"\\\0-\x1F\x7F]+)*"'
NUMBER = r'-?(0|[1-9][0-9]*)(\.[0-9]+)?([eE][+-]?[0-9]+)?'
IDENTIFIER = r'[^\d\W]\w*'

DIRECTIVE = '@'
LAMBDA_OPEN = '('
LAMBDA_CLOSE = ')'
BLOCK_OPEN = <indent up>
BLOCK_CLOSE = <indent down>
COMMENT = r'//.*\n'

// inside `array`, `object` and `partial` the indentation has no effect