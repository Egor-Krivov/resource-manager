config = importWholeConfig* partialImport* definition*

importWholeConfig = importStr | importStarred
importStr = 'import' STRING
importStarred = 'from' (IDENTIFIER | '.')? '.' dotted 'import' '*'

partialImport = importSimple | importFrom | importFromConfig
importSimple = 'import' dotted_import_as (',' dotted_import_as)*
importFrom = 'from' dotted 'import' many_import_as
importFromConfig = 'from' ('.')? '.' dotted 'import' many_import_as

dotted = IDENTIFIER ('.' IDENTIFIER)*
import_as = IDENTIFIER (as IDENTIFIER)?
many_import_as = import_as (',' import_as)*
dotted_import_as = dotted (as IDENTIFIER)?

definition = IDENTIFIER '=' expression

expression = data (getAttribute | call | getItem)*
getItem = '[' expression (',' expression)* ','? ']'
getAttribute = '.' IDENTIFIER
call = '(' params ')'
params = LAZY? (positional | keyword | (positional ',' keyword) ','?)?
positional = '*'? expression (',' '*'? expression)*
keyword = definition (',' definition)*

data = lambda | dictionary | array | tuple | IDENTIFIER | STRING | NUMBER | 'None' | 'False' | 'True'
dictionary = '{' (pair (',' pair)* ','?)? '}'
pair = expression ':' expression
array = '[' (expression (',' expression)* ','?)? ']'
tuple = '(' (expression (',' expression)* ','?)? ')'
# TODO: add varargs and kwargs
lambda = 'lambda' (IDENTIFIER (',' IDENTIFIER)*)? ':' expression

LAZY = '#\s*lazy\s*$'

// this is an approximate regex. the true one is simply imported from `tokenize`
STRING = r'''(\"\"\"|\'\'\'|\"|\')((?<!\\)(\\\\)*\\\1]|.)*?\1'''
NUMBER = r'-?(0|[1-9][0-9]*)(\.[0-9]+)?([eE][+-]?[0-9]+)?'
IDENTIFIER = r'[^\d\W]\w*'
COMMENT = r'#.*$'
